"
An OBTitlePageTemplate is used to generate the title page of the PDF generated by Ozono.
"
Class {
	#name : #OBTitlePageTemplate,
	#superclass : #OBAbstractPageTemplate,
	#category : #'ObjectBrowser-PDFGeneration'
}

{ #category : #builder }
OBTitlePageTemplate >> buildOn: pdfDoc [
	page := PDFPage new.	
	pdfDoc add: page.
	
	self createTitle. 
	self createAuthor. 
	self createCourse.
	self createTeacher.
	self createDate.

]

{ #category : #builder }
OBTitlePageTemplate >> createAuthor [
	self createLabel: 'Autor:' withText: (self lessonToPDF author)  withHeight: 15 onTop: 120.
]

{ #category : #builder }
OBTitlePageTemplate >> createCourse [
	self createLabel: 'Curso:' withText: (self lessonToPDF course)  withHeight: 15 onTop: 135.
]

{ #category : #builder }
OBTitlePageTemplate >> createDate [
	self createLabel: 'Fecha:' withText: (self lessonToPDF formattedDate)  withHeight: 15 onTop: 165.
]

{ #category : #builder }
OBTitlePageTemplate >> createTeacher [
	self createLabel: 'Profesor:' withText: (self lessonToPDF teacher)  withHeight: 15 onTop: 150.
]

{ #category : #builder }
OBTitlePageTemplate >> createTitle [
	| height top |
	height := 50.
	top := 50.
	
	page
		add:
			(PDFParagraphElement new
				styleSheet: self titleStyleSheet;
				dimension: self usableWidth mm @ height mm;
				from: self leftMargin mm @ top mm;
				text: self lessonToPDF lesson title)
]

{ #category : #styles }
OBTitlePageTemplate >> labelStyleSheet [
	| titleFont titleStyleSheet |
	titleFont := (PDFTimesFont new fontSize: 24 pt) bold.
	titleStyleSheet := PDFStyleSheet new font: titleFont.
	titleStyleSheet alignment: PDFAlignment left middle.
	^titleStyleSheet.
]

{ #category : #styles }
OBTitlePageTemplate >> titleStyleSheet [
	| titleFont titleStyleSheet |
	titleFont := (PDFHelveticaFont new fontSize: 48 pt) bold italic.
	titleStyleSheet := PDFStyleSheet new font: titleFont.
	titleStyleSheet alignment: PDFAlignment center middle.
	^titleStyleSheet.
]

{ #category : #styles }
OBTitlePageTemplate >> valueStyleSheet [
	| titleFont titleStyleSheet |
	titleFont := (PDFTimesFont new fontSize: 22 pt).
	titleStyleSheet := PDFStyleSheet new font: titleFont.
	titleStyleSheet alignment: PDFAlignment left middle.
	^titleStyleSheet.
]
